configurations {
    jaxb
}
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
        classpath("com.oracle.ojdbc:ojdbc8:19.3.0.0")
    }
}

group 'ru.progmatik'
version '0.3.0'

apply plugin: 'java'
apply plugin: 'idea'
// assembly to win application
//apply plugin 'application'
//apply plugin 'com.google.cloud.tools.jib:1.8.1'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        resources {
            srcDirs = [rootDir, "${rootDir}/src/main/resources"]
            includes = ["README.md", "*.ico", "*.yml", "*.txt"]
        }
    }
}
repositories {
    mavenCentral()
}

// tag::wsdl[]
task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "https://fias.nalog.ru/WebServices/Public/DownloadService.asmx?wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "fias.wsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}
// end::wsdl[]

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")

    // https://mvnrepository.com/artifact/com.github.junrar/junrar
    compile group: 'com.github.junrar', name: 'junrar', version: '4.0.0'

    compile ("org.postgresql:postgresql:42.2.2")
    compile ("org.firebirdsql.jdbc:jaybird-jdk18:3.0.4")
    compile ("com.oracle.ojdbc:ojdbc8:19.3.0.0")

    compile("org.springframework.ws:spring-ws-core")

    compile(files(genJaxb.classesDir).builtBy(genJaxb))

// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.8'

    jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bootJar {
    baseName = 'fiasloader'
    version =  '0.3.0'

    from genJaxb.classesDir
}

// Docker
/*
jib {
    allowInsecureRegistry=true
    from {
        image='bellsoft/liberica-openjdk-alpine-musl:11'
    }
    to {
        image='localhost:5000/fiasloader:1.0.0'
        tags=['latest']
    }
    container{
        mainClass='FiasLoaderMainClass'
        args='fiasloaderApp'
        ports=['8080']
    }
}
 */